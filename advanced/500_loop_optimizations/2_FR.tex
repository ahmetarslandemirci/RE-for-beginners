\subsection{Autre optimisation de boucle}

Si vous traitez tous les éléments d'un tableau qui est situé dans la mémoire globale,
le compilateur peut l'optimiser.
Par exemple, calculons la somme de tous les éléments du tableau de 128 \IT{int}:

\begin{lstlisting}[style=customc]
#include <stdio.h>

int a[128];

int sum_of_a()
{
	int rt=0;
	
	for (int i=0; i<128; i++)
		rt=rt+a[i];

	return rt;
};

int main()
{
	// initialize
	for (int i=0; i<128; i++)
		a[i]=i;
	
	// calculate the sum
	printf ("%d\n", sum_of_a());
};
\end{lstlisting}

GCC 5.3.1 (x86) \Optimizing peut produire ceci (\IDA):

\begin{lstlisting}[style=customasmx86]
.text:080484B0 sum_of_a        proc near
.text:080484B0                 mov     edx, offset a
.text:080484B5                 xor     eax, eax
.text:080484B7                 mov     esi, esi
.text:080484B9                 lea     edi, [edi+0]
.text:080484C0
.text:080484C0 loc_80484C0:            ; CODE XREF: sum_of_a+1B
.text:080484C0                 add     eax, [edx]
.text:080484C2                 add     edx, 4
.text:080484C5                 cmp     edx, offset __libc_start_main@@GLIBC_2_0
.text:080484CB                 jnz     short loc_80484C0
.text:080484CD                 rep retn
.text:080484CD sum_of_a        endp
.text:080484CD

...

.bss:0804A040                 public a
.bss:0804A040 a               dd 80h dup(?) ; DATA XREF: main:loc_8048338
.bss:0804A040                               ; main+19
.bss:0804A040 _bss            ends
.bss:0804A040
extern:0804A240 ; ===========================================================================
extern:0804A240
extern:0804A240 ; Segment type: Externs
extern:0804A240 ; extern
extern:0804A240                 extrn __libc_start_main@@GLIBC_2_0:near
extern:0804A240                            ; DATA XREF: main+25
extern:0804A240                            ; main+5D
extern:0804A244                 extrn __printf_chk@@GLIBC_2_3_4:near
extern:0804A248                 extrn __libc_start_main:near
extern:0804A248                            ; CODE XREF: ___libc_start_main
extern:0804A248                            ; DATA XREF: .got.plt:off_804A00C
\end{lstlisting}

Qu'est-ce que c'est que \TT{\_\_libc\_start\_main@@GLIBC\_2\_0} en \TT{0x080484C5}?
Ceci est un label situé juste après la fin du tableau \TT{a[]}.
Cette fonction peut être récrite comme ceci:

\begin{lstlisting}[style=customc]
int sum_of_a_v2()
{
	int *tmp=a;
	int rt=0;
	
	do
	{
		rt=rt+(*tmp);
		tmp++;
	}
	while (tmp<(a+128));

	return rt;
};
\end{lstlisting}

La première version a le compteur \IT{i}, et l'adresse de chaque élément du tableau
est calculée à chaque itération.
La seconde version est plus optimisée: le pointeur sur chaque élément du tableau
est toujours prêt et est déplacé de 4 octets à chaque itération.
Comment vérifier si la boucle est terminée?
Il suffit de comparer le pointeur avec l'adresse juste après la fin du tableau, qui
est dans notre cas l'adresse de la fonction \TT{\_\_libc\_start\_main()} importée de la Glibc 2.0.
Parfois ce genre de code est perturbant, et ceci est une astuce d'optimisation très
répandue, c'est pourquoi j'ai mis cet exemple.

Ma seconde version est très proche de ce que fait GCC, et lorsque je la compile,
le code est presque le même que dans la première version, mais les deux première
instructions sont échangées:

\begin{lstlisting}[style=customasmx86]
.text:080484D0                 public sum_of_a_v2
.text:080484D0 sum_of_a_v2     proc near
.text:080484D0                 xor     eax, eax
.text:080484D2                 mov     edx, offset a
.text:080484D7                 mov     esi, esi
.text:080484D9                 lea     edi, [edi+0]
.text:080484E0
.text:080484E0 loc_80484E0:            ; CODE XREF: sum_of_a_v2+1B
.text:080484E0                 add     eax, [edx]
.text:080484E2                 add     edx, 4
.text:080484E5                 cmp     edx, offset __libc_start_main@@GLIBC_2_0
.text:080484EB                 jnz     short loc_80484E0
.text:080484ED                 rep retn
.text:080484ED sum_of_a_v2     endp
\end{lstlisting}

Inutile de dire que cette optimisation n'est possible que si le compilateur peut
calculer l'adresse de la fin du tableau pendant la compilation.
Ceci se produit si le tableau est global et sa taille fixée.

Toutefois, si l'adresse du tableau est inconnue lors de la compilation, mais que la
taille est fixée, l'adresse du label juste après la fin du tableau peut être calculée.
% FIXME <!-- \ref{} to example -->

